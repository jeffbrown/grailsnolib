buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "commons-io:commons-io:1.4"
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'

grailsVersion = '1.4.0.BUILD-SNAPSHOT'
version = grailsVersion

sourceCompatibility = "1.5"
targetCompatibility = "1.5"
archivesBaseName = 'grails'

configurations {
    compile.transitive = false
    jsp21.extendsFrom compile
}

repositories {
	mavenRepo urls:"http://maven.springframework.org/release"
}

sourceSets {
    main {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs = ['src/java']
        }
        resources {
            srcDirs = ['src/java']
            include '**/*.properties', '**/*.xsd', '**/*.dtd'
        }
    }
    jsp21 {
        compileClasspath = files(configurations.jsp21, main.classes)
        groovy.srcDirs = ['src/jsp21']
        resources.srcDirs = []
    }
    test {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs = ['src/test']
        }
        resources {
            srcDirs = ['src/test', 'src/jar']
            exclude '**/*.java', '**/*.groovy'
        }
    }
    buildScripts {
        compileClasspath = sourceSets.main.compileClasspath + main.classes
        classesDir = "$buildDir/compiled-scripts" as File
        groovy.srcDirs = ['scripts']
        resources.srcDirs = []
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
	apply plugin: 'idea'


	manifest.mainAttributes(
	        "Built-By": System.properties['user.name'],
	        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
	        "Implementation-Title": "Grails",
	        "Ant-Version": 'Apache Ant 1.7.1', // todo we just have this property to make it easy to compare manifests. Remove it when this build is life
	        "Implementation-Version": version,
	        "Implementation-Vendor": 'grails.org')
	
    group = "org.grails"
	version = grailsVersion
    archivesBaseName = "grails"

	configurations {
	    compile.transitive = true
	    jsp21.extendsFrom compile
	}
	repositories {
		mavenRepo urls:"http://maven.springframework.org/release"
	}	
	dependencies {
       	groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.0-beta-4'

		// Jars in lib
		compile files(fileTree(dir:"$projectDir/src/libs" as File))

		// Ant
		def antVersion = "1.7.1"
		compile "org.apache.ant:ant:${antVersion}",
	 			"org.apache.ant:ant-launcher:${antVersion}",
				"org.apache.ant:ant-junit:${antVersion}",	
				"org.apache.ant:ant-nodeps:${antVersion}",			
				"org.apache.ant:ant-trax:${antVersion}",
				"org.apache.ivy:ivy:2.2.0",
				'org.codehaus.gant:gant_groovy1.7:1.9.3'
				

		// Apache Commons
		compile 'commons-beanutils:commons-beanutils:1.8.0'	,
				'commons-cli:commons-cli:1.0',
				'commons-codec:commons-codec:1.4',
				'commons-collections:commons-collections:3.2.1',
				'commons-dbcp:commons-dbcp:1.3',
				'commons-el:commons-el:1.0',
				'commons-fileupload:commons-fileupload:1.2.1',
				'commons-io:commons-io:1.4',
				'commons-lang:commons-lang:2.4',
				'commons-pool:commons-pool:1.5.5',
				'commons-validator:commons-validator:1.3.1'

		// Others
		compile( 'cglib:cglib-nodep:2.1_3',
				 'org.fusesource.jansi:jansi:1.2.1',
				 'oro:oro:2.0.8',
				 'jline:jline:0.9.94',
				 'opensymphony:sitemesh:2.4',
				 'org.tmatesoft.svnkit:svnkit:1.3.4',
				 'ognl:ognl:2.7.3',
				 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.1_jdk5') 

		runtime 'aopalliance:aopalliance:1.0',
	  		    'com.h2database:h2:1.2.147',
				'hsqldb:hsqldb:1.8.0.10',
				'org.aspectj:aspectjrt:1.6.10',
				'org.aspectj:aspectjweaver:1.6.10',
				'net.sf.ehcache:ehcache-core:2.3.1',
				'org.coconut.forkjoin:jsr166y:070108'


		// Testing
		compile 'junit:junit:4.8.1'

		// XML
		compile 'xpp3:xpp3_min:1.1.4c'
		compile 'xalan:serializer:2.7.1'

		// Logging
		compile 'log4j:log4j:1.2.16'
		compile 'org.slf4j:slf4j-log4j12:1.6.1'
		compile 'org.slf4j:slf4j-api:1.6.1'
		compile 'org.slf4j:jcl-over-slf4j:1.6.1'
		compile 'org.slf4j:jul-to-slf4j:1.6.1'	


		// Specs
		compile 'javax.servlet:jsp-api:2.0'
		compile 'javax.servlet:servlet-api:2.5'
		compile 'javax.servlet:jstl:1.2'
		compile 'taglibs:standard:1.1.2'
		compile 'net.sf.jsr107cache:jsr107cache:1.0'
		compile 'javax.transaction:jta:1.1'
		compile 'javax.persistence:persistence-api:1.0'

		// Hibernate related
		compile 'org.hibernate:hibernate-annotations:3.4.0.GA',
				'org.hibernate:hibernate-commons-annotations:3.1.0.GA',
				'org.hibernate:hibernate-core:3.3.1.GA',
				'org.hibernate:hibernate-validator:3.1.0.GA',
				'org.hibernate:hibernate-ehcache:3.3.1.GA',
				'javassist:javassist:3.11.0.GA',
				'antlr:antlr:2.7.6'

		// Spring
		def springVersion = "3.0.3.RELEASE"
		compile "org.springframework:spring-aop:${springVersion}",
				"org.springframework:spring-asm:${springVersion}",
				"org.springframework:spring-aspects:${springVersion}",
				"org.springframework:spring-beans:${springVersion}",
				"org.springframework:spring-context:${springVersion}",
				"org.springframework:spring-core:${springVersion}",					
				"org.springframework:spring-expression:${springVersion}",								
				"org.springframework:spring-instrument:${springVersion}",								
				"org.springframework:spring-jdbc:${springVersion}",					
				"org.springframework:spring-jms:${springVersion}",					
				"org.springframework:spring-orm:${springVersion}",
				"org.springframework:spring-oxm:${springVersion}",					
				"org.springframework:spring-test:${springVersion}",					
				"org.springframework:spring-tx:${springVersion}",
				"org.springframework:spring-web:${springVersion}",
				"org.springframework:spring-webmvc:${springVersion}"


		// Groovy
	    groovy "org.codehaus.groovy:groovy-all:1.8.0-beta-4"
		compile( 'org.codehaus.gpars:gpars:0.9' ) {
			exclude module:'netty'
		}

	    jsp21 'javax.servlet.jsp:jsp-api:2.1'
	}

	
}

jsp21Classes.dependsOn classes
task allClasses(dependsOn: [classes, jsp21Classes, buildScriptsClasses])

configure([compileGroovy, compileTestGroovy]) {
    groovyOptions.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')
    groovyOptions.encoding = "UTF-8"
}

configure([compileJava, compileTestJava]) {
    options.deprecation = true
    options.debug = true
}

apply {
    from 'gradle/docs.gradle'
    from 'gradle/assemble.gradle'
/*    from 'gradle/unit-test.gradle'
    from 'gradle/maven.gradle'
    from 'gradle/findbugs.gradle'*/
}

clean {
    // add additional dirs
    delete(docs.destinationDir, libs.destinationDir)
}

